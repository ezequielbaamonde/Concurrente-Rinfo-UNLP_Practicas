programa etapas
procesos
  proceso juntarPapeles
  comenzar
    mientras HayPapelEnLaEsquina
      tomarPapel
  fin
  proceso juntarFlores
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
  fin
  proceso recorrerCalle
  comenzar
    repetir 9
      juntarPapeles
      juntarFlores
      mover
  fin
  proceso depositar
  comenzar
    mientras HayFlorEnLaBolsa
      depositarFlor
    mientras HayPapelEnLaBolsa
      depositarPapel
  fin
  proceso avisar(ES id: numero)
  comenzar
    si id=1
      EnviarMensaje(V, r2)
      EnviarMensaje(V, r3)
    sino
      si id=2
        EnviarMensaje(V, r1)
        EnviarMensaje(V, r3)
      sino
        EnviarMensaje(V, r1)
        EnviarMensaje(V, r2)
  fin
  proceso esperar(E id: numero)
  variables
    ok: boolean
  comenzar
    si id=1
      RecibirMensaje(ok, r2)
      RecibirMensaje(ok, r3)
    sino
      si id=2
        RecibirMensaje(ok, r1)
        RecibirMensaje(ok, r3)
      sino
        RecibirMensaje(ok, r1)
        RecibirMensaje(ok, r2)
  fin
areas
  deposito: AreaC (50,50,50,50)
  ca1: AreaP(1,1,100,1)
  ca2: AreaP(1,2,100,2)
  ca3: AreaP(1,3,100,3)
  iniFisca: AreaP(1,4,1,4)
robots
  robot recolectores
  variables
    id, avAct, caAct: numero
  comenzar
    caAct:= PosCa
    RecibirMensaje(id, fisca)
    derecha
    mientras (PosAv<>100)
      recorrerCalle
      avAct:= PosAv
      si (HayFlorEnLaBolsa) | (HayPapelEnLaBolsa)
        BloquearEsquina(50,50)
        Pos(50,50)
        depositar
        Pos(avAct, caAct)
        LiberarEsquina(50,50)
      avisar(id)
      esperar(id)
    EnviarMensaje(V, fisca) {se avisa que terminamos todo}
  fin
  
  robot fiscalizador
  variables
    id, flores: numero
    ok: boolean
  comenzar
    flores:= 0
    EnviarMensaje(1, r1)
    EnviarMensaje(2, r2)  
    EnviarMensaje(3, r3)
    repetir 3
      RecibirMensaje(ok, *)
    Pos(50,50)
    mientras HayFlorEnLaEsquina
      tomarFlor
      flores:= flores + 1
    Informar('Cantidad de flores juntadas ', flores)
  fin
variables
  r1: recolectores
  r2: recolectores
  r3: recolectores
  fisca: fiscalizador
comenzar
  AsignarArea(fisca, iniFisca)
  AsignarArea(fisca, deposito)
  AsignarArea(r1, ca1)
  AsignarArea(r1, deposito)
  AsignarArea(r2, ca2)
  AsignarArea(r2, deposito)
  AsignarArea(r3, ca3)
  AsignarArea(r3, deposito)
  Iniciar(fisca, 1,4)
  Iniciar(r1, 1,1)
  Iniciar(r2, 1,2)
  Iniciar(r3, 1,3)
fin